-- This requires the preloading file `config.cafe`.

-- load config.cafe .

** Showing a state in which the predicate `auth` does not hold.
open PQSAFE-CONFIG .
-- a: Alice, b: Bob
  ops a b : -> Prin .
  op cs : -> Suite .
  op css : -> Suites .
  op i : -> Sid .
  ops k k2 : -> EcSecretK .
  ops k' k2' : -> PqSecretK .
  op kems : -> PqKemParams .
  ops pk pk2 : -> EcPublicK .
  op pk' : -> PqPublicK .
  op en : -> PqCipher .
  ops cspk cspk2 : -> SignPubK .
  ops r r2 : -> Rand .
  ops s0 s1 s2 s3 s4 s5 s6 s7 s8 s9 s10 s11 s12 s13 s14 s15 s16 s17 s18 s19 s20 : -> Sys .
  ops sign sign2 sign$ sign2$ cfin sfin signk : -> Data .
  op v : -> Version .
  op dl : -> DataL .

-- config
  eq (a = intru) = false .
  eq (b = intru) = false .
  eq (a = b) = false .
  eq (r = r2) = false .
  eq (k = k2) = false .
  eq (k' = k2') = false .
  eq contains(css,cs) = true .

-- macros
  eq pk = ecPublic(k) .
  eq pk' = keygen(k') .
  eq pk2 = ecPublic(k2) .
  eq en = encapsC(keygen(k'),k2') .
  eq cspk = signPubK(b) .
  eq cspk2 = signPubK(a) .
  eq sign = sign(signPriK(ca), h(b || signPubK(b))) .
  eq sign2 = sign(signPriK(ca), h(a || signPubK(a))) .
  eq signk = sign(signPriK(b), h(r || r2 || pk || pk')) .
  eq dl = i || v || r || css || kems || r2 || cs .
  eq cfin = enc(hsckey(ms((k | k2) || (k' & k2'), 
      r || r2 || pk2 || en), r || r2),
    h(a || b || dl || cspk || sign || pk || pk' || signk || pk2 || en)) .
  eq sfin = enc(hsskey(ms((k | k2) || (k' & k2'), 
      r || r2 || pk2 || en), r || r2),
    h(a || b || dl || cspk || sign || pk || pk' || signk || pk2 || en)) .

** transitions leading to the state disproving `auth`
-- a sends ClientHello to b, and then
-- b sends ServerHello and Certificate to a
  eq s0 = init .
  eq s1 = chello(s0,a,b,v,r,css,kems) .
  eq s2 = shello(s1,r2,cs,i,a,a,b,v,r,css,kems) .
  eq s3 = scert(s2,a,b,v,r2,cs,i) .

-- intruder breaks classical signature algorithm to get b's private signing key
  eq s4 = brkSign(s3,b) .

-- intruder impersonates b in communication with a
-- intruder sends ServerHello and Certificate again
  eq s5 = fkHello(s4,b,a, v || r2 || cs || i) .
  eq s6 = fkScert(s5,b,a,cspk,sign) .
-- intruder generates two ephemeral secret keys, and
-- signs them by b's private signing key
  eq s7 = gEcSecretK(s6,k) .
  eq s8 = gPqSecretK(s7,k') .
  eq s9 = g1(s8, r || r2 || pk || pk') .
  eq s10 = gSign(s9, signPriK(b), h(r || r2 || pk || pk')) .
-- intruder acts as b to send ServerKeyExchange message to a
  eq s11 = fkSkeyex(s10,b,a,pk,pk',signk) .
  eq s12 = fkNoti(s11,b,a) .

-- a sends ClientKeyExchange and Finished messages to b
  eq s13 = ccert(s12,a,b,v,r,css,kems,
    intru,r2,cs,i,cspk,sign,pk,pk',signk,0) .
  eq s14 = ckeyex(s13,k2,k2',a,b,v,r,css,kems,
    intru,r2,cs,i,cspk,sign,pk,pk',signk,0) .
  eq s15 = certVer(s14,a,b,v,r,css,kems,intru,r2,cs,i,
    cspk,sign,pk,pk',signk,0,k2,k2',1) .
  eq s16 = cfinish(s15,a,b,v,r,css,kems,intru,r2,cs,i,
    cspk,sign,pk,pk',signk,0,k2,k2',1) .

  red auth(s16,a,b,a,pk,pk',k2,k2',r,r2,cspk,sign,signk,cspk2,sign2,dl,0) .

-- the following is the only skexM in the network:
-- skexM(intru,b,a,ecPublic(k) || keygen(k') ||
--     sign(signPriK(b), h(r || r2 || ecPublic(k) || keygen(k'))), 0)
  red nw(s16) .
close

eof
load original-protocol/primitives.cafe original-protocol/protocol.cafe .
load original-protocol/with-client-auth/pqtls-ca.cafe .
load original-protocol/with-client-auth/properties.cafe .
load original-protocol/with-client-auth/pq-safe/config.cafe .
load original-protocol/with-client-auth/pq-safe/auth-counterexample.cafe .